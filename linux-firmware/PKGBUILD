# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=linux-firmware
_commit=2a713be25a44bd6cec90d8affc54b246a2ca9c7b # master
pkgver=20180119.2a713be
pkgrel=1
pkgdesc="Firmware files for Linux"
makedepends=('git')
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
conflicts=('linux-firmware-git'
           'kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'amd-ucode')
replaces=('kernel26-firmware'
          'ar9170-fw'
          'iwlwifi-1000-ucode'
          'iwlwifi-3945-ucode'
          'iwlwifi-4965-ucode'
          'iwlwifi-5000-ucode'
          'iwlwifi-5150-ucode'
          'iwlwifi-6000-ucode'
          'rt2870usb-fw'
          'rt2x00-rt61-fw'
          'rt2x00-rt71w-fw'
          'amd-ucode')
options=(!strip)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git#commit=${_rev}"
        "iwlwifi-update-16-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=c48ebaf4b31bad10c1d5203706a2e5960da4fc5c"
        "iwlwifi-add-14-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=6ba88f9586a9b3b79d6278b11a2cf04732151013"
        "iwlwifi-add-15-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=ca2c1a9c426ab0eb4c4a7d44335ca647b143531f"
        "iwlwifi-add-17-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=833d464fc077d653d8e728d3d4c2b3d98ba581fd"
        "iwlwifi-add-18-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=c8bc8058fc9089b73a85f301ec26eaa1daec38ac"
        "iwlwifi-add-19-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=219d52e7ad7e9ea6f806d7abfc2918cd999f3acb"
        "iwlwifi-add-20-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=7129ac81a733762e2cddd6e294c4d77c0a3e6c71"
        "iwlwifi-update-22-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=685548cc4795ede5c924cfea9c0274b98ab8b0d8"
        "iwlwifi-update-17-ucode.patch::https://git.kernel.org/cgit/linux/kernel/git/iwlwifi/linux-firmware.git/patch/?id=be8a6fcba2e48e00674d1f35e6def56e2268039c"
        'microcode_amd_fam17h.bin')
md5sums=('SKIP'
         '81ef67b100c72c133a5fde379b980ded'
         '63fdcdce7ccdcbcf8beda0b2443f0ce8'
         '86775dadd2784a200917bfbc622cd238'
         'be41227c79e2fbe78d3bffafb19cc054'
         '401f012748792b0648a9cfc0fb43a7b9'
         'ddd3eb8f6267a415e16f7698cc261e02'
         'e97092be36e0dc5232758bc3090d0a80'
         'b5bc0dbee285dcd659435a61c015052c'
         '5fa1a287aef54e70457418eda8e025c4'
         '4610fdded46938a4b094c5897e5c92b2')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i "${srcdir}/iwlwifi-update-16-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-add-14-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-add-15-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-add-17-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-add-18-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-add-19-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-add-20-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-update-22-ucode.patch"
  patch -p1 -i "${srcdir}/iwlwifi-update-17-ucode.patch"
}

pkgver() {
  cd "${srcdir}/${pkgname}"

  # Commit date + short rev
  echo $(TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" FIRMWAREDIR=/usr/lib/firmware install
  rm "${pkgdir}/usr/lib/firmware/"{Makefile,README,configure,GPL-3}

  cp "${srcdir}/microcode_amd_fam17h.bin" "${pkgdir}/usr/lib/firmware/amd-ucode/"

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICEN* WHENCE "${pkgdir}/usr/share/licenses/linux-firmware/"

  # Trigger a microcode reload for configurations not using early updates
  install -d "${pkgdir}/usr/lib/tmpfiles.d"
  echo 'w /sys/devices/system/cpu/microcode/reload - - - - 1' \
    >"${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
# vim:set ts=2 sw=2 et:
